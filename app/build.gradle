apply plugin: 'com.android.application'
apply plugin: "org.ajoberstar.grgit"

import org.ajoberstar.grgit.Grgit

ext {
    //Git tag as version name, using org.ajoberstar.grgit.Grgit
    git = Grgit.open()
    tags = git.tag.list()
    gitVersionCode = tags.size()
    gitVersionName = tags[tags.size()-1].getName()
}

android {
    compileSdkVersion 28
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.github.ttdyce.nhviewer"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    def isRunningOnTravis = System.getenv("CI") == "true"

    if (isRunningOnTravis) {
        // configure keystore
        signingConfigs.release.storeFile = file("myapps.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }
}

dependencies {
    def nav_version = "2.1.0"
    def room_version = "2.2.0-rc01"
    def preference_version = "1.1.0"

    //image transformations for Glide (for blur image)
    implementation 'jp.wasabeef:glide-transformations:4.1.0'
    //QRCode scanner
    implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:2.0.3'
    //Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.google.firebase:firebase-config:19.0.2'
    //ROOM
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    //preferenceFragment
    implementation "androidx.preference:preference:$preference_version"
    //for Jetpack navigation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.android.volley:volley:1.1.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'org.json:json:20190722'//for unit test using json
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
